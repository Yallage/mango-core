plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'com.yallage.mango'
version '1.0.0-dev'

sourceCompatibility = 11
targetCompatibility = 11

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

    // https://hub.spigotmc.org/nexus/content/repositories/snapshots/
    implementation 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    // https://mvnrepository.com/artifact/net.md-5/bungeecord-api
    implementation 'net.md-5:bungeecord-api:1.16-R0.4'

    // https://mvnrepository.com/artifact/org.fusesource.jansi/jansi
    implementation 'org.fusesource.jansi:jansi:2.4.0'

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation 'com.google.code.gson:gson:2.9.0'

    // https://mvnrepository.com/artifact/org.mongodb/mongo-java-driver
    implementation 'org.mongodb:mongo-java-driver:3.12.10'
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.convention('sources')
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.convention('javadocs')
    archiveClassifier.set('javadocs')
}

javadoc {
    options.addBooleanOption('html5', true)
    options.encoding('utf-8')
}

shadowJar {
    manifest {
        attributes(
                'Main-Class': 'com.yallage.mango.core.MangoCore',
                "Multi-Release": true
        )
    }
    dependencies {
        include(dependency('org.mongodb:mongo-java-driver:3.12.10'))
    }
}

publishing {
    repositories {
        maven {
            url = uri((project.findProperty("repository_root_url") ?: System.getenv("REPOSITORY_ROOT_URL"))
                    + (project.version.toString().endsWith('release') ? 'releases' : 'snapshots'))
            credentials {
                username = project.findProperty("repository_user") ?: System.getenv("REPOSITORY_USER")
                password = project.findProperty("repository_token") ?: System.getenv("REPOSITORY_TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifact(jar)
            artifact(sourcesJar)
            artifact(javadocJar)
        }
    }
}